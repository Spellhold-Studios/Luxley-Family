BACKUP ~weidu_external/luxleysoa/backup~

//AUTHOR ~feuille_sakaki@spellholdstudios.net~
SUPPORT ~https://github.com/Spellhold-Studios/Luxley-Family~

VERSION ~v2.1.0~

README ~luxleysoa/readme/luxleysoa-readme-english.html~

ALWAYS

  ACTION_IF NOT VARIABLE_IS_SET bg2_chapter BEGIN // Always block must run only once per install!

    /* ----------------------------------------- *
     *    Determines which game is installed.    *
     * ----------------------------------------- */

    OUTER_SET is_tob   = (GAME_IS ~bgt tob tutu tutu_totsc~) ? 1 : 0     // based on vanilla ToB engine
    OUTER_SET is_bg2ee = (GAME_IS "bg2ee") ? 1 : 0                       // b2gee
    OUTER_SET is_eet   = (GAME_IS "eet") ? 1 : 0                         // eet
    OUTER_SET is_ee    = (GAME_IS ~bg2ee eet~) ? 1 : 0                   // EE game
    OUTER_SET is_tobex = (FILE_EXISTS ~tobex_ini/tobexcore.ini~) ? 1 : 0 // ToBEx is installed

    /* ------------------------------------------------------- *
     *    Sets default variables if they're not set already    *
     * ------------------------------------------------------- */

    // Variable used to properly install specific EE games ressources
    ACTION_IF (is_ee) BEGIN
      OUTER_SPRINT ee_file "_ee"
    END ELSE BEGIN
      OUTER_SPRINT ee_file ""
    END

    // Variable used to properly install Sebastian kit
    ACTION_IF (is_ee OR is_tobex) BEGIN
      OUTER_SET kits_lim_256  = 0
    END ELSE BEGIN
      OUTER_SET kits_lim_256  = 1
    END

    /* ------------------------------------------- *
     *    EET chapters continuity compatibility    *
     * ------------------------------------------- */
    ACTION_IF is_eet BEGIN
      OUTER_SET bg2_chapter = 12
    END ELSE BEGIN
      OUTER_SET bg2_chapter = 0
    END
    OUTER_FOR (i = 1 ; i <= 10 ; ++i) BEGIN
      OUTER_SET bg2_chapter = bg2_chapter + 1
      OUTER_SPRINT name_source ~bg2_chapter_%i%~
      OUTER_SET EVAL ~%name_source%~ = bg2_chapter
    END

    /* --------------------------------------------------------------------------------------------- *
     *    Convert strings from UTF-8 for original games                                              *
     *    No exception, everything needs to be converted                                             *
     *    setup.tra contains in game texts as well as installation texts and needs to be converted   *
     * --------------------------------------------------------------------------------------------- */
    ACTION_DEFINE_ARRAY fhl#reload BEGIN setup END

    LAF HANDLE_CHARSETS
      INT_VAR
        infer_charsets    = 1
        from_utf8         = 1
      STR_VAR
        default_language  = ~english~
        tra_path          = ~luxleysoa/lang~
        out_path          = ~weidu_external/luxleysoa/lang~
        reload_array      = ~fhl#reload~
    END

    ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
      OUTER_TEXT_SPRINT tra_location ~luxleysoa/lang~
    END ELSE BEGIN
      OUTER_TEXT_SPRINT tra_location ~weidu_external/luxleysoa/lang~
    END

  END

END

AUTO_TRA ~%tra_location%/%s~

// Languages
LANGUAGE ~English~
         ~english~
         ~luxleysoa/lang/english/setup.tra~

LANGUAGE ~Russian (translation by Austin)~
         ~russian~
         ~luxleysoa/lang/english/setup.tra~
         ~luxleysoa/lang/russian/setup.tra~



/* ====================================================== *
 *    Luxley Family Mod for BGII:SoA  (main component)    *
 * ====================================================== */

BEGIN @1 // ~Luxley Family Mod for BGII:SoA~
LABEL ~fhl_luxley_main~
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @2 // ~You must have BG2, ToB, BGT, BG2:EE or EET installed to install the Luxley Family mod.~

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~



/* ===================================================================================================================================== *
 *    Portraits for non-joinable NPCs  (optional component)  :  install it if you want custom portraits for various non-joinable NPCs    *
 * ===================================================================================================================================== */

BEGIN @300 // ~Portraits for non-joinable NPCs~
LABEL ~fhl_luxley_portraits~
//REQUIRE_COMPONENT ~luxleysoa.tp2~ ~0~ @25 // ~You must have the Jerry Zinger Show installed.~
//REQUIRE_PREDICATE (MOD_IS_INSTALLED ~luxleysoa.tp2~ 0) @22
REQUIRE_PREDICATE MOD_IS_INSTALLED ~luxleysoa.tp2~ (ID_OF_LABEL ~luxleysoa.tp2~ ~fhl_luxley_main~) @3 // ~You must have the Luxley Family installed.~

COPY ~%MOD_FOLDER%/portraits_others~ ~override~



/* =================================================================================================================================== *
 *    Nathaniel Crossmod content  (optional component)  :  these will be deprecated by a future release of the Crossmod Banter Pack    *
 * =================================================================================================================================== */

BEGIN @400                             // ~Nathaniel Crossmod content~
LABEL ~fhl_luxley_nathaniel_crossmod_content~
REQUIRE_PREDICATE MOD_IS_INSTALLED ~luxleysoa.tp2~ (ID_OF_LABEL ~luxleysoa.tp2~ ~fhl_luxley_main~) @3   // ~You must have the Luxley Family installed.~
REQUIRE_FILE ~override/fhnat.dlg~                                                                  @401 // ~You do not appear to have Nathaniel installed.~

COMPILE ~%MOD_FOLDER%/crossmod/fhlnath.d~
EXTEND_BOTTOM ~fhnat.bcs~ ~%MOD_FOLDER%/crossmod/fhlnath.baf~
